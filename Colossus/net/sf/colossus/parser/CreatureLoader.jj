options
{
  IGNORE_CASE = false;
  STATIC = false;
}

PARSER_BEGIN(CreatureLoader)
package net.sf.colossus.parser;


import java.util.*;

import net.sf.colossus.util.Log;
import net.sf.colossus.server.Creature;
import net.sf.colossus.server.VariantSupport;

/**
 * CreatureLoader the creatures descriptions.
 * @author Romain Dolbeau
 * @version $Id$
 * @see net.sf.colossus.server.Creature
 */
public class CreatureLoader
{
}

PARSER_END(CreatureLoader)

SKIP :
{
    " "
  | "\r"
  | "\t"
}

TOKEN :
{
        < COMMENT : "#"(<NOTNEWLINE>)*"\r" | "#"(<NOTNEWLINE>)*"\n" >
|       < #NOTNEWLINE : ~["\n","\r"] >
}

String c_comment() :
{}
{
    <COMMENT>
    {
        return(new String(token.image));
    }
}

TOKEN :
{
    < EOL: "\n" >
}

TOKEN :
{
    < NUMBER : "-"(<DIGIT>)+|(<DIGIT>)+ >
|   < #DIGIT : ["0" - "9"] >
}

TOKEN :
{
    < BOOL: "true"|"false" >
}

TOKEN :
{
    < SPECIALCHAINE : "Special:" (<NUMCAR>)+ >
|   < CHAINE : <CAR> (<NUMCAR>)* >
|   < QUOTEDCHAINE : "\"" (<QUOTEDCAR>)+ "\"" >
|   < #CAR : ["a"-"z","A"-"Z",".","_"] >
|   < #NUMCAR : <CAR>|["0"-"9"] >
|   < #QUOTEDCAR : <NUMCAR>|" " >
}

boolean c_bool() :
{}
{
    <BOOL>
    { if (token.image.equals("true")) return true; else return false; }
}

String c_specialchaine() :
{}
{
    <SPECIALCHAINE>
    {
        return(new String(token.image));
    }
}

String c_chaine() :
{}
{
    <CHAINE>
    {
        return(new String(token.image));
    }
|   <QUOTEDCHAINE>
    {
        String tok = new String(token.image);
        String cha = tok.substring(1, tok.length() - 1);
        return(cha);
    }
}

int c_number() :
{}
{
    <NUMBER>
    {
        return(Integer.parseInt(token.image));
    }
}

/**
 * Add one creature to the list of creature.
 * @param lc List of creatures.
 * @return Status of the parser ; negative at the end of file, positive on success, null on blank line.
 * @see net.sf.colossus.server.Creature
 */
int oneCreature(List lc) :
{
    String name;
    String pluralName;
    int power;
    int skill;
    boolean rangestrikes;
    boolean flies;
    boolean nativeBramble;
    boolean nativeDrift;
    boolean nativeBog;
    boolean nativeSandDune;
    boolean nativeSlope;
    boolean nativeVolcano;
    boolean nativeRiver;
    boolean nativeStone;
    boolean nativeTree;
    boolean waterDwelling;
    boolean magicMissile;
    boolean summonable;
    boolean lord;
    boolean demilord;
    int maxCount;
    String baseColor = null;
    String s;
    String classtouse = null;
}
{
    (name = c_chaine())
    (power= c_number())
    (skill= c_number())
    (rangestrikes= c_bool())
    (flies= c_bool())
    (nativeBramble= c_bool())
    (nativeDrift= c_bool())
    (nativeBog= c_bool())
    (nativeSandDune= c_bool())
    (nativeSlope= c_bool())
    (nativeVolcano= c_bool())
    (nativeRiver= c_bool())
    (nativeStone= c_bool())
    (nativeTree= c_bool())
    (waterDwelling= c_bool())
    (magicMissile= c_bool())
    (summonable= c_bool())
    (lord= c_bool())
    (demilord= c_bool())
    (maxCount= c_number())
    (pluralName = c_chaine())
    (baseColor = c_chaine())?
    (classtouse = c_specialchaine())?
    {
        if (name.equals(net.sf.colossus.server.Constants.titan))
        {
            if (classtouse != null)
            {
                Log.warn("Can't specify a custom class for Titan, ignoring \""
                         + classtouse + "\"");
            }
            classtouse = "Special:net.sf.colossus.server.CreatureTitan";
        }
        if (classtouse == null) // nice and easy way
        {
            lc.add(new Creature(name, power, skill, rangestrikes,
                                flies, nativeBramble, nativeDrift,
                                nativeBog, nativeSandDune, nativeSlope,
                                nativeVolcano, nativeRiver, nativeStone,
                                nativeTree,
                                waterDwelling, magicMissile, summonable,
                                lord, demilord, maxCount, pluralName,
                                baseColor));
        }
        else // you want to play hard ? let's play hard.
        {
            // first, build an array of from the various parameter.
            Object o[] = new Object[22];
            o[0] = name;
            o[1] = new Integer(power);
            o[2] = new Integer(skill);
            o[3] = new Boolean(rangestrikes);
            o[4] = new Boolean(flies);
            o[5] = new Boolean(nativeBramble);
            o[6] = new Boolean(nativeDrift);
            o[7] = new Boolean(nativeBog);
            o[8] = new Boolean(nativeSandDune);
            o[9] = new Boolean(nativeSlope);
            o[10] = new Boolean(nativeVolcano);
            o[11] = new Boolean(nativeRiver);
            o[12] = new Boolean(nativeStone);
            o[13] = new Boolean(nativeTree);
            o[14] = new Boolean(waterDwelling);
            o[15] = new Boolean(magicMissile);
            o[16] = new Boolean(summonable);
            o[17] = new Boolean(lord);
            o[18] = new Boolean(demilord);
            o[19] = new Integer(maxCount);
            o[20] = pluralName;
            o[21] = baseColor;
            Object cre = net.sf.colossus.util.ResourceLoader.getNewObject(classtouse.substring(8), VariantSupport.getVarDirectoriesList(), o);
            if (!(cre instanceof Creature))
            {
                Log.error("CUSTOM: Class \"" + classtouse +
                          "\" cannot be used to make a Creature !");
                return 0;
            }
            else
            {
                Log.debug("CUSTOM: Class \"" + classtouse +
                          "\" successfully loaded for creature " + name);
            }
            lc.add((Creature)cre);
        }
        System.out.println(name + " is a " + power + " " +
                           (flies ? "*" : "") +
                           (rangestrikes ? "~" : "") + " " +
                           skill);
        return 1;
    }
|   <EOL>
    {
        return(0);
    }
|   (s = c_comment())
    {
        return(0);
    }
|   <EOF>
    {
        return(-1);
    }
}
